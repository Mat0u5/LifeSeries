plugins {
 id 'fabric-loom' version "${loom_version}"
 id 'maven-publish'
 id "me.modmuss50.mod-publish-plugin" version "1.0.0"
}

static boolean isValidVersion(String string) {
 return string != null && !string.isEmpty() && !string.equalsIgnoreCase("null") && !string.equalsIgnoreCase("[VERSIONED]")
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
 archivesName = project.archives_base_name
}
loom {
 splitEnvironmentSourceSets()

 mods {
  "lifeseries" {
   sourceSet sourceSets.main
   sourceSet sourceSets.client
  }
 }

 fabricModJsonPath = rootProject.file("src/main/resources/fabric.mod.json")
}
repositories {
 maven { url "https://api.modrinth.com/maven" }
 maven { url 'https://maven.nucleoid.xyz' }
 maven { url "https://maven.tomalbrc.de" }
 maven {
  name = 'Ladysnake Mods'
  url = 'https://maven.ladysnake.org/releases'
 }
 maven { url "https://maven.shedaniel.me/" }
 maven { url = 'https://maven.maxhenkel.de/repository/public' }
}

fabricApi {
 configureDataGeneration()
}
dependencies {
 minecraft("com.mojang:minecraft:${property("minecraft_version")}")
 mappings loom.officialMojangMappings()

 modImplementation("net.fabricmc:fabric-loader:${property("loader_version")}")
 modImplementation("net.fabricmc.fabric-api:fabric-api:${property("fabric_version")}")

 // Compile only for compatibility
 modCompileOnly "maven.modrinth:flashback:z0SX4zNw"

 // Runtime only QOL mods
 if (isValidVersion(project.carpet_bot_relog_version)) modRuntimeOnly "maven.modrinth:carpet-bot-relog:${project.carpet_bot_relog_version}"
 if (isValidVersion(project.voicechat_mod_version)) modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${project.voicechat_mod_version}"
 if (isValidVersion(project.carpet_mod_version)) modRuntimeOnly "maven.modrinth:carpet:${project.carpet_mod_version}"

 //Runtime and compile
 if (isValidVersion(project.voicechat_api_version)) implementation "de.maxhenkel.voicechat:voicechat-api:${project.voicechat_api_version}"
}
loom {
 runConfigs.all {
  //ideConfigGenerated true // Run configurations are not created for subprojects by default
  runDir "../../run" // Use a shared run folder and create separate worlds
 }
}
def java21 = stonecutter.eval(stonecutter.current.version, ">=1.20.5")

processResources {
 filesMatching("fabric.mod.json") {
  expand "version": project.property('mod_version'),
          "targetVersion": project.property('target_version'),
          "fabricApiVersion": project.property('fabric_api_target_version'),
          "minecraftVersion": project.property('minecraft_version'),
          "javaVersion": project.property('java_version')
 }
 filesMatching("lifeseries.mixins.json") {
  expand "javaVersion": java21 ? "JAVA_21":"JAVA_17"
 }
}
processClientResources {
 filesMatching("lifeseries.client.mixins.json") {
  expand "javaVersion": java21 ? "JAVA_21":"JAVA_17"
 }
}

tasks.withType(JavaCompile).configureEach {
 it.options.release = java21 ? 21 : 17
}

java {
 def javaVersion = java21 ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
 targetCompatibility = javaVersion
 sourceCompatibility = javaVersion
}

jar {
 from("LICENSE") {
  rename { "${it}_${project.base.archivesName.get()}"}
 }
}

publishMods {
 file = remapJar.archiveFile
 changelog = providers.environmentVariable("CHANGELOG")
 type = providers.environmentVariable("VERSION_TYPE")
 displayName = "Life Series ${project.property('mod_version')} for Fabric ${project.property('min_version')}${project.property('min_version') == project.property('max_version') ? '' : '-' + project.property('max_version')}"

 modLoaders.add("fabric")

 def isDevBuild = providers.environmentVariable("DEV_BUILD").getOrElse("true") == "true"
 def shouldIncludeSnapshots =
         project.property('min_version').toString().contains("w") || project.property('max_version').toString().contains("w") ||
         project.property('min_version').toString().contains("pre") || project.property('max_version').toString().contains("pre") ||
         project.property('min_version').toString().contains("rc") || project.property('max_version').toString().contains("rc")

 modrinth {
  projectId = isDevBuild ? "RLDqKhd4" : "aLasQi8P"
  accessToken = providers.environmentVariable("MODRINTH_TOKEN")
  minecraftVersionRange {
   start = project.property('min_version')
   end = project.property('max_version')
   includeSnapshots = shouldIncludeSnapshots
  }

  requires("fabric-api")
  optional("simple-voice-chat")
 }

 if (!isDevBuild) {
  curseforge {
   projectId = "1151647"
   accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
   minecraftVersionRange {
    start = project.property('min_version')
    end = project.property('max_version')
   }

   clientRequired = true
   serverRequired = true

   requires("fabric-api")
   optional("simple-voice-chat")
  }
 }
}

// configure the maven publication
publishing {
 publications {
  create("mavenJava", MavenPublication) {
   artifactId = project.archives_base_name
   from components.java
  }
 }

 // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
 repositories {
  // Add repositories to publish to here.
  // Notice: This block does NOT have the same function as the block in the top level.
  // The repositories here will be used for publishing your artifact, not for
  // retrieving dependencies.
 }
}

task runClientNamed {
 doFirst {
  runClient.args('--username', 'Player')
 }
 finalizedBy 'runClient'
}